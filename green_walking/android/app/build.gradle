plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"

    // START: FlutterFire Configuration
    id "com.google.gms.google-services" // required by firebase, firebase_crashlytics
    id "com.google.firebase.crashlytics"
    // END: FlutterFire Configuration
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}


android {
    kotlinOptions {
        // Added due to https://github.com/flutter/flutter/issues/147185
        jvmTarget = '1.8'
    }

    compileSdkVersion 36

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }


    defaultConfig {
        // Unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "org.xennis.apps.green_walking"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lint {
        disable 'InvalidPackage'
    }
    namespace 'org.xennis.apps.green_walking'
}

flutter {
    source '../..'
}

dependencies {
    // firesbase stack leads to > 65K methods. Hence https://developer.android.com/studio/build/multidex is needed.
    // That's default for minSdkVersion >= 21.
    implementation 'androidx.multidex:multidex:2.0.1'

    // https://firebase.google.com/docs/android/learn-more#bom
    // When using the BoM, you don't specify versions in Firebase library dependencies.
    //implementation platform('com.google.firebase:firebase-bom:29.0.0')
    //implementation 'com.google.firebase:firebase-crashlytics'
}
